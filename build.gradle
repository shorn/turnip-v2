plugins{
  id 'application'
}

group 'cloud.kopi.turnip'
version '1.0-SNAPSHOT'

repositories{
  mavenCentral()

  // needed for resolving spring-6 milestone builds
  maven { url "https://repo.spring.io/libs-milestone" }
}

sourceSets {
  funcTest { }
}

configurations {
  funcTestImplementation.extendsFrom(testImplementation)
}

java{
  toolchain{
    languageVersion = JavaLanguageVersion.of(17)
  }
}

// compile funcTest classes - but don't run by default (needs Auth0 setup)
tasks.build.dependsOn funcTestClasses

tasks.withType(JavaCompile) {
  // show details of deprecated usage
  options.compilerArgs += "-Xlint:deprecation"
  // show details of unchecked or unsafe operations
  options.compilerArgs += "-Xlint:unchecked"
  // warn about dodgy finalizers
  options.compilerArgs += "-Xlint:finally"

  // UTF-8 everywhere - yes, even (especially!) when compiling 
  // your IDE should be set to UTF-8, too
  options.encoding = "UTF-8"
}

dependencies{
  // use SLf4J API to log against - pkg: org.slf4j
  // using 2.0.0 for turnip code because that's what jetty uses
  implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'

  // look in settings.gradle for version catalog specs 
  implementation libs.bundles.jetty11
  implementation libs.bundles.spring6

  // support for JSON serialization for Spring endpoints
  // can this be "runtimeOnly"?
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
  
  // integrate Auth0 JWT into spring-security
  // this library is Spring-5 based and will cause "NoSuchMethodError" when
  // it tries to use javax.servlet stuff.
  implementation 'com.auth0:auth0-spring-security-api:1.5.1'
  // pkg: com.auth0.jwt
  implementation 'com.auth0:java-jwt:3.19.2'

  // use the standard jar instead of binding unnecessarily to Jetty
  // pkg: jakarta.servlet - used to be javax.servlet  
  compileOnly 'jakarta.platform:jakarta.jakartaee-web-api:9.1.0'

  // intially for tests to talk to Auth0, but I'm sure I'll end up using it 
  runtimeOnly 'org.apache.httpcomponents.client5:httpclient5-win:5.1.3'

  // use logback at runtime for Turnip, Jetty, Spring, etc.
  runtimeOnly 'ch.qos.logback:logback-core:1.3.0-alpha16'
  runtimeOnly 'ch.qos.logback:logback-classic:1.3.0-alpha16'

  // for PostConstruct/PreDestroy - pkg: javax.annotation
  implementation 'javax.annotation:javax.annotation-api:1.3.2'

  // junit 5 - pkg: org.junit.jupiter
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

  // for integrating junit and spring - pkg: org.springframework.test
  testImplementation 'org.springframework:spring-test:6.0.0-M5'
  testImplementation "org.assertj:assertj-core:3.23.1"

  // for compilation depending on prod classes   
  funcTestImplementation sourceSets.main.output
  
  /* for compilation depending on test util classes; without this , funcTest 
  will fail to compile, can't find stuff like BDD */
  funcTestImplementation sourceSets.test.output
  
  /* without this, funcTest runtime fails to find logback-test.xml when run
  from IDEA. IDE issue only -  gradle `funcTest` task works fine. */
  funcTestRuntimeOnly sourceSets.test.runtimeClasspath
}

application{
  mainClass = 'turnip.App'
}

applicationDefaultJvmArgs = [
  // UTC everywhere, always
  '-Duser.timezone=UTC',
  // UTF-8 everywhere, always
  "-Dfile.encoding=UTF-8",
  // for consistency, rather than any specific reason 
  "-Duser.language=", "-Duser.country=", "-Duser.variant=",
]

test{
  // execute tests even if no prod or test code has changed
  outputs.upToDateWhen {false}

  useJUnitPlatform()
  jvmArgs += applicationDefaultJvmArgs
  testLogging.showStandardStreams = true
}

task funcTest(type: Test) {
  description = "Run FUNCtional tests"
  group = "verification"

  testClassesDirs = sourceSets.funcTest.output.classesDirs
  classpath += sourceSets.funcTest.runtimeClasspath

  // execute tests even if no prod or test code has changed
  outputs.upToDateWhen {false}

  testLogging{
    showStandardStreams = true
    exceptionFormat = 'full'
  }

  // `set()` is just to suppress IDEA warning
  javaLauncher.set(javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(17) 
  })

  useJUnitPlatform()

  jvmArgs += applicationDefaultJvmArgs
}

task uberJar(type: Jar) {
  group "distribution"
  // using `=`, IDEA error warn about archiveClassifier exceeds access rights  
  archiveClassifier.set('uber')

  // different name from standard jar and don't want version in the name
  archiveFileName = "turnip-app.jar"

  //noinspection GroovyAssignabilityCheck
  manifest{
    mainClassName = "turnip.App"
    //noinspection GroovyAssignabilityCheck
    duplicatesStrategy "EXCLUDE"
    attributes (
      'Main-Class': 'turnip.App',
      "Implementation-Title": "Turnip",
      "Implementation-Version": archiveVersion,
    )
  }
  
  dependsOn configurations.runtimeClasspath
  from sourceSets.main.output
  from {
    configurations.runtimeClasspath.findAll { 
      it.name.endsWith('jar') }.collect { zipTree(it) 
    }
  }
  
  doFirst{
    println "generating uberJar to ${archiveFile.get()}"
  }
}

// to upgrade the wrapper, change the version and run this task
tasks.named('wrapper') {
  distributionType = Wrapper.DistributionType.ALL
  gradleVersion = '7.5'
}

